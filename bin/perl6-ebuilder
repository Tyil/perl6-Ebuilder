#! /usr/bun/env perl6

use v6.c;

use Ebuilder;
use JSON::Fast;
use Template::Mustache;

sub MAIN(*@files)
{
	for @files -> $input {
		my IO $file = $input.IO;

		if (!$file.e) {
			say "No such file or directory: $file";
			next;
		}

		my %meta = from-json($file.slurp);
		my Str $output;

		try {
			CATCH {
				default {
					.say;
					next;
				}
			}

			$output = make-ebuild(%meta);
		}

		say $output;
	}
}

sub make-ebuild(%meta --> Str)
{
	# Check for required elements
	my Str @required-fields = (
		"description",
		"license",
		"name",
		"source-url",
		"version",
	);

	for @required-fields -> $field {
		if (%meta{$field}:!exists) {
			die "Missing required field from META6.json: $field";
		}
	}

	# List all dependencies
	my Str @dependencies;

	for %meta<depends>.list -> $dependency {
		@dependencies.push("dev-perl/{ebuild-name($dependency)}-9999");
	}

	# Generate ebuild
	my Str %context =
		dependencies => @dependencies.join(" "),
		description => %meta<description>,
		license => %meta<license>,
		name => ebuild-name(%meta<name>),
		src_uri => %meta<source-url>,
	;

	Template::Mustache.render(template, %context).trim;
}

sub ebuild-name(Str $name) returns Str
{
	"p6-" ~ $name.lc.subst('::', '-', :g)
}
