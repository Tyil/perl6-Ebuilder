#! /usr/bin/env perl6

use v6.c;

use Ebuilder;
use HTTP::UserAgent;

sub MAIN
{
	# Get ecosystem META.list
	my HTTP::UserAgent $ua .= new;
	my $response = $ua.get("https://raw.githubusercontent.com/perl6/ecosystem/master/META.list");

	if (!$response.is-success) {
		die $response.status-line;
	}

	my Str $meta-list = $response.content;

	for $meta-list.lines -> $line {
		try {
			CATCH {
				default {
					say "Failed $line: " ~ .Str;
					next;
				}
			}

			# Get meta
			my $meta-response = $ua.get($line);

			if (!$meta-response.is-success) {
				die $meta-response.status-line;
			}

			my %meta = from-json($meta-response.content);

			# TODO
			# Check the git repo itself to find out if there's tagged releases,
			# and create a specific ebuild for every release if they have

			my Str $ebuild = make-ebuild(%meta);
			my Str $atom-name = atom-name(%meta<name>, 9999);

			# Save ebuild
			spurt("{$atom-name}.ebuild", $ebuild);

			# Rejoice
			say "Saved $atom-name";
		}
	}
}
